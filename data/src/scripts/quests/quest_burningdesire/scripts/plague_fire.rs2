[oploc1,plague_fire]
if (%burning_desire_progress < 2) {
    if (npc_find(0_49_50_46_17, bon_frere, 10, 0) = true) {
        @bon_frere_get_away_from_that;
    }
} else if (%burning_desire_progress = 2) {
    @burning_desire_use_logs_with_fire;
} else if (%burning_desire_progress > 2) {
    ~chatplayer("<p,quiz>Perhaps I shouldn't mess with Bon Frere's fire anymore.");
}

[label,bon_frere_get_away_from_that]
~chatnpc("<p,angry>Please do not touch the holy Flame!|I'm doing important work here!");

[label,burning_desire_use_logs_with_fire]
def_obj $best_logs = ~best_logs;
if($best_logs = null) {
    mes("You do not have any logs of which you have the level to use.");
    return;
}

if (%action_delay < map_clock) {
    p_stopaction;
    anim(human_pickuptable, 0);
    sound_synth(fire_lit, 0, 0);
    inv_del(inv, $best_logs, 1);
    givexp(firemaking, oc_param($best_logs, productexp));
    mes("You add the <lowercase(oc_name($best_logs))> strengthening the fire.");
    %burning_desire_progress = ^burning_desire_burn_rats;
    %action_delay = calc(map_clock + 4);
}


[oplocu,plague_fire]
switch_obj(last_useitem) {
    case plague_rat_corpse : @burning_desire_burn_plague_corpse_single(last_useitem);
    case default : mes("Nothing interesting happens."); // osrs
}

[oploc3,plague_fire]// @burning_desire_burn_plague_corpse(last_useitem);


[label,burning_desire_burn_plague_corpse_single](obj $plague_corpse)
if (%burning_desire_plague_rats_count > 1 & %burning_desire_progress = ^burning_desire_burn_rats) {
    %burning_desire_progress = ^burning_desire_burnt_rats;
}

if (%burning_desire_progress = ^burning_desire_complete) {
    ~chatplayer("<p,default>I should make my own fire instead.");
    return;
}

if (%burning_desire_progress < ^burning_desire_burn_rats ) {
    if (npc_find(0_49_50_46_17, bon_frere, 10, 0) = true) {
        @bon_frere_get_away_from_that;
    }
}

if (%burning_desire_progress >= ^burning_desire_burnt_rats) {
    ~chatplayer("<p,quiz>I should probably speak with Bon Frere.");
    return;
}

if(inv_total(inv, $plague_corpse) = null) {
    mes("You're out of <lowercase(oc_name($plague_corpse))>'.");
    return;
}

if (%action_delay <= map_clock) {
    p_stopaction;

    anim(human_pickuptable, 0);
    sound_synth(fire_lit, 0, 0);

    inv_del(inv, $plague_corpse, 1);

    mes("You toss the <lowercase(oc_name($plague_corpse))> into the fire.");
    givexp(firemaking, oc_param($plague_corpse, firemaking_exp));
    givexp(prayer, oc_param($plague_corpse, prayer_exp));
    %burning_desire_plague_rats_count = calc(%burning_desire_plague_rats_count + 1);

    %action_delay = calc(map_clock + 4);
}